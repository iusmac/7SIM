import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        // https://mvnrepository.com/artifact/androidx.room/room-compiler
        // https://mvnrepository.com/artifact/androidx.room/room-runtime
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/heads/main/current/androidx/m2repository/androidx/room/room-compiler/
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/heads/main/current/androidx/m2repository/androidx/room/room-runtime/
        def room_version = '2.5.0-alpha01'
        // https://mvnrepository.com/artifact/com.google.dagger/hilt-android
        // https://mvnrepository.com/artifact/com.google.dagger/hilt-compiler
        // https://android.googlesource.com/platform/external/dagger2
        def hilt_version = '2.35.1'
        libs = [
            // https://mvnrepository.com/artifact/androidx.core/core
            // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/heads/main/current/androidx/m2repository/androidx/core/core/
            core                 :  'androidx.core:core:1.9.0-alpha05',
            hilt_android         :  "com.google.dagger:hilt-android:$hilt_version",
            hilt_compiler        :  "com.google.dagger:hilt-compiler:$hilt_version",
            room_compiler        :  "androidx.room:room-compiler:$room_version",
            room_runtime         :  "androidx.room:room-runtime:$room_version",
        ]
    }
}

plugins {
    // https://mvnrepository.com/artifact/com.android.application/com.android.application.gradle.plugin
    id 'com.android.application' version '7.4.2' apply false
    // https://mvnrepository.com/artifact/com.android.library/com.android.library.gradle.plugin
    id 'com.android.library' version '7.4.2' apply false
    // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.android
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
}

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'com.github.iusmac.sevensim'
    compileSdk 33

    defaultConfig {
        applicationId 'com.github.iusmac.sevensim'
        minSdk 24
        targetSdk 33
        versionName '1.0.0'
        versionCode 1
    }

    final def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(file('keystore.properties')))
    def releaseSigning = signingConfigs.create('release') {
        keyAlias keystoreProperties['keyAlias']
        keyPassword keystoreProperties['keyPassword']
        storeFile file(keystoreProperties['storeFile'])
        storePassword keystoreProperties['storePassword']
    }

    buildTypes {
        all {
            signingConfig releaseSigning
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.flags'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions.jvmTarget = JvmTarget.JVM_9
    }

    sourceSets {
        main {
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    lintOptions {
        // Instead of failing the build due to missing translations, just inform us to avoid
        // mistakes during manual import
        informational 'MissingTranslation'
    }

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
}

dependencies {
    implementation libs.core

    configurations {
        all {
            // Fix Room v2.5.0-alpha01 library duplicate dependencies
            // Ref: https://issuetracker.google.com/issues/242384116
            exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        }
    }

    implementation libs.hilt_android
    kapt libs.hilt_compiler

    implementation libs.room_runtime
    kapt libs.room_compiler
}

kapt {
    correctErrorTypes true
}
